test:
	cargo tarpaulin --out Lcov --all-features

format:
	cargo fmt --all

lint:
	cargo clippy --all-targets -- -D warnings

# Prepare for a new release
pre-release:
    #!/usr/bin/env bash
    set -e

    if [[ -n $(git status --porcelain) ]]; then
      echo "Git working directory is not clean. Please commit or stash your changes."
      exit 1
    fi

    echo "Running checks and tests..."
    just lint
    just test

    current_version=$(echo "$(cargo pkgid --quiet | cut -d '@' -f 2)")
    echo "Current version is ${current_version}"

    if [[ $current_version != *"-SNAPSHOT"* ]]; then
      echo "Error: Current version is not a SNAPSHOT version."
      exit 1
    fi

    new_version=$(echo ${current_version} | sed 's/-SNAPSHOT//')
    echo "Bumping version to ${new_version}..."
    cargo set-version ${new_version}
    cargo update

    echo "Updating kaizen_website version in README.md"
    sed -i "s/kaizen_parser-v[0-9.]*/kaizen_parser-v${new_version}/" ../README.md

    echo "Committing version bump..."
    git add Cargo.toml Cargo.lock ../README.md
    git commit -m "chore(release): prepare parser for release v${new_version}"

    echo "Pre-release for version ${new_version} is ready."
    echo "You can now push the changes to trigger the release workflow."